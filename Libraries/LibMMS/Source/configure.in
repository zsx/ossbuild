AC_INIT([libmms], [0.4], [sh@warma.dk])
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER([config.h src/mms_config.h])
AC_CONFIG_SRCDIR(configure.in)

AC_ISC_POSIX
AC_CANONICAL_HOST
AC_PROG_CC
AC_STDC_HEADERS
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AC_PROG_LIBTOOL
AC_PROG_INSTALL


PKG_CHECK_MODULES(GLIB, glib-2.0)
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

dnl
dnl check for large file support
dnl hacky, copied from gstreamer's configure.ac

AC_MSG_CHECKING(for large file support)
AC_TRY_RUN([
#define _LARGEFILE_SOURCE
#define _FILE_OFFSET_BITS 64
#include <sys/types.h>
int main () { return !(sizeof(off_t) == 8); }
],
[
  AC_MSG_RESULT(yes)
  MMS_PKG_CFLAGS="-D_LARGEFILE_SOURCE -D_FILE_OFFSET_BITS=64"
  echo "#define LIBMMS_HAVE_64BIT_OFF_T 1" >> confdefs.h
],
[
  AC_MSG_RESULT(no)
],
[
  AC_MSG_RESULT(no)
])

dnl Checks for header files.
AC_CHECK_HEADERS([sys/socket.h netinet/in.h netdb.h windows.h winsock2.h])

case $host in
    *beos*)
        AC_DEFINE([BEOS],1,[Define if compiling on BeOS system.])
        ;;
    *mingw*)
        MMS_LIBS="$MMS_LIBS -lws2_32"
        ;;
esac

AC_SUBST(MMS_CFLAGS)
AC_SUBST(MMS_PKG_CFLAGS)
AC_SUBST(MMS_LIBS)


AC_CONFIG_FILES([
Makefile
pkgconfig/Makefile
pkgconfig/libmms.pc
src/Makefile
libmms.spec])
AC_OUTPUT

echo "configure done"
